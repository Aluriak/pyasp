% test consitenstency of influence graph with experimental observations

input(P,V) :- input(V), exp(P).
exp(P) :- obs_vlabel(P,V,S).
exp("empty") :- 0{obs_vlabel(P,V,S)}0.

% The following code constitutes the core part of consistency checking.

1{vlabel(P,V,1), vlabel(P,V,-1), vlabel(P,V,0)}1 :- exp(P), vertex(V), not input(P,V).
1{vlabel(P,U,1), vlabel(P,U,-1), vlabel(P,U,0)}1 :- exp(P), edge(U,V), not input(P,V).
1{elabel(U,V,1), elabel(U,V,-1)}1 :- exp(P), edge(U,V), not input(P,V).

vlabel(P,V,S) :- vertex(V), obs_vlabel(P,V,S).
elabel(U,V,S) :- edge(U,V), obs_elabel(U,V,S).

infl(P,V,1) :- elabel(U,V,1), vlabel(P,U,1).
infl(P,V,1) :- elabel(U,V,-1), vlabel(P,U,-1).
infl(P,V,-1) :- elabel(U,V,-1), vlabel(P,U,1).
infl(P,V,-1) :- elabel(U,V,1), vlabel(P,U,-1).


% increase and decrease must be explained
:- vlabel(P,gen(V),1),  not infl(P,gen(V),1),  not input(P,gen(V)).
:- vlabel(P,gen(V),-1),  not infl(P,gen(V),-1),  not input(P,gen(V)).

% no variation must {not} be explained
:- vlabel(P,gen(V),0),  infl(P,gen(V),-1), not infl(P,gen(V),1),  not input(P,gen(V)).
:- vlabel(P,gen(V),0),  infl(P,gen(V),1),  not infl(P,gen(V),-1), not input(P,gen(V)).
% :- vlabel(P,gen(V),0),  infl(P,gen(V),-1),  not input(P,gen(V)).
% :- vlabel(P,gen(V),0),  infl(P,gen(V),1), not input(P,gen(V)).


#hide.
%#show infl/3.
%#show elabel/3.
%#show vlabel/3.
